buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 5
        versionName "5prealpha"
        setProperty("archivesBaseName", "pocketsphinx-android-$versionName")
	externalNativeBuild {
	    cmake {
	    }
	}
        ndk {
	    abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
	}
    }
    sourceSets {
         main {
              java.srcDirs = ['src/main/java', 'build/generated-src']
         }
    }
    externalNativeBuild {
	cmake {
    	    path "CMakeLists.txt"
	}
    }
}

task mkdir << {
    new File('build/generated-src/java').mkdirs()
    new File('build/generated-src/cpp').mkdirs()
}

task swigSb(type: Exec) {
    commandLine 'swig',
	"-I../sphinxbase/include", "-I../sphinxbase/swig", 
	"-java", "-package", "edu.cmu.pocketsphinx", 
	"-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/sphinxbase_wrap.c",
	"../sphinxbase/swig/sphinxbase.i"
}

task swigPs(type: Exec) {
    commandLine 'swig',
        "-I../sphinxbase/swig",
        "-I../pocketsphinx/include",
        "-I../pocketsphinx/swig",
        "-java", "-package", "edu.cmu.pocketsphinx",
	"-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/pocketsphinx_wrap.c",
        "../pocketsphinx/swig/pocketsphinx.i"
}

preBuild.dependsOn mkdir, swigSb, swigPs
